// classes act as a struct where properties can be defined
// they do not have methods yet, but it's still possible to
// define them with first class functions and closures

fun newLinkedList() {
  class LinkedList {
  // head -> Node | nil
  // tail -> Node | nil
  // length() -> number
  // push(data: Object) -> nil
  }

  var length = 0;
  var ll = LinkedList();

  ll.head = nil;
  ll.tail = nil;

  ll.length = fun () { return length; };
  ll.push = fun (data) {
    var node = newNode(data, nil);

    if (ll.head == nil) {
      ll.head = node;
      ll.tail = ll.head;
    }
    else {
      ll.tail.next = node;
      ll.tail = node;
    }

    length = length + 1;
  };

  return ll;
}

fun newNode(data, next) {
  class Node {
  // data -> Object
  // next -> Node
  }
  var node = Node();
  node.data = data;
  node.next = next;
  return node;
}

var ll = newLinkedList();
ll.push("a");
ll.push("b");
ll.push("c");
ll.push("d");

print(ll.length());

var curr = ll.head;
while (curr != nil) {
  print(curr.data);
  curr = curr.next;
}
